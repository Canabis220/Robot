FUNCTION "Conveer8PO" : Void
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1

BEGIN
	//линия 1
	
	CASE "Conveer_status"."08_step" OF//
	    0://стоп
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        
	        IF "SW8_1" = TRUE THEN //если на входе нет  тогда другой шаг
	            "Conveer_status"."08_step" := 10;
	        END_IF;
	        
	        IF "SW8_1" = false THEN //если на входе есть , то след шаг
	            "Conveer_status"."08_step" := 20;
	        END_IF;
	        
	        ;
	    10://нет на входе
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        "Robot16".TDI02 := FALSE;// палета не на месте
	        
	        IF "SW8_1" = FALSE
	            AND "Robot16".FDO02 = TRUE THEN //если на входе есть и робот поставил новую паллету, то след шаг
	            "Conveer_status"."08_step" := 20;
	        END_IF;
	        
	        IF "resetSW8" THEN
	            "Conveer_status"."08_step" := 0;
	        END_IF;
	        
	        ;
	    20://есть на входе и не готово
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := TRUE; //вытолкнуть
	        
	        IF "SW8_6" = TRUE THEN
	            "KA8_2_auto" := TRUE; //зафиксировать
	        END_IF;
	        
	        IF "SW8_8" = TRUE THEN
	            "Conveer_status"."08_step" := 22;//уходим на укладку
	        END_IF;
	        ;
	    22://укладка
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := TRUE; //вытолкнуть
	        "KA8_2_auto" := TRUE; //зафиксировать
	        "Robot16".TDI02 := TRUE;// палета на месте
	        
	        IF "Robot16".FDO10 = TRUE THEN
	            //если робот закончил, тогда готово
	            "Conveer_status"."08_step" := 28;
	        END_IF;
	        
	        IF "SW8_1" = TRUE THEN
	            //если на входе нет, тогда предошибка
	            "Conveer_status"."08_step" := 50;
	        END_IF;
	        
	        ;
	    28://готово
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не зафиксировать
	        
	        IF "SW8_7" = TRUE THEN
	            //если разжали, тогда выезжаем
	            "Conveer_status"."08_step" := 30;
	        END_IF;
	        
	        ;
	        
	    30://загружаем
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        
	        IF "SW8_4" = TRUE  //если кара уехала 
	            AND "SW8_3" = TRUE THEN//и нет выехавшей паллеты
	            "FA8_1_auto_start" := TRUE;//двигатель транспортера в автомате работает
	        ELSE    //если кара близко
	            "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        END_IF;
	        
	        IF "SW8_3" = FALSE//если  на выходе есть, 
	            AND "SW8_1" = TRUE THEN //а на входе нет, тогда проверка
	            "Conveer_status"."08_step" := 40;
	        END_IF;
	        ;
	    40://подсчет и проверка
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        
	        IF "SW8_3" = FALSE THEN //если  на выходе есть, то все проверили
	            "Conveer_status"."08_step" := 0;
	        END_IF;
	        
	        ;
	    50://предошибка
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        "Robot16".TDI02 := FALSE;// палета не на месте
	        
	        IF "Robot16".FDO17 = FALSE
	        THEN //если  то 
	            "Conveer_status"."08_step" := 60;// ошибка
	        END_IF;
	        
	        IF "resetSW8" THEN
	            "Conveer_status"."08_step" := 0;
	        END_IF;
	        
	        
	        ;
	    60://ошибка
	        "FA8_1_auto_start" := FALSE;//двигатель транспортера в автомате не работает
	        "KA8_1_auto" := FALSE; //не вытолкнуть
	        "KA8_2_auto" := FALSE; //не фиксировать
	        
	        IF "resetSW8" THEN
	            "Conveer_status"."08_step" := 0;
	        END_IF;
	        
	        ;
	        
	    70://возврата нет
	        "Conveer_status"."08_step" := 0;
	        ;
	        
	        ;
	    ELSE:
	        ;
	END_CASE;
	    
	    IF "Auto3" THEN
	        "KA8_1" := "KA8_1_auto";
	        "KA8_2" := "KA8_2_auto";
	    END_IF;
	    
END_FUNCTION

