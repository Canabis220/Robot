FUNCTION "Robot12_main" : Void
{ S7_Optimized_Access := 'FALSE' }
VERSION : 0.1

BEGIN
NETWORK
TITLE = РТК1 - Закрыть захват продукта
      AN "Auto1";
      JC M1;
      A "Robot12".FDO05;
      A "Auto1";
      = "КА12_5";

      A "Robot12".FDO04;
      A "Auto1";
      = "КА12_4";

NETWORK
TITLE = РТК1 - Закрыть захват паллеты
      A "Robot12".FDO07;
      A "Auto1";
      = "КА12_3";

      A "Robot12".FDO06;
      A "Auto1";
      = "КА12_2";
NETWORK
TITLE = РТК1 - Включить вакуум.
      A "Robot12".FDO08;
      A "Auto1";
      = "КА12_1";
NETWORK
TITLE = РТК1 - Захват картона опустить
      A "Robot12".FDO19;
      A "Auto1";
      = "КА12_6";

      A "Robot12".FDO20;
      A "Auto1";
      = "КА12_7";

NETWORK
TITLE = DI2 Паллета на месте 1
M1:      AN "SW3_1"            ;//есть палета
      A "SW3_8";// есть захват
      S "Robot12".TDI02;//палета на месте

      A "SW3_1";//нет палеты
      A "SW3_2";// нет первого ряда
      AN "SW3_8";// нет захвата
      AN "SW3_3"            ;//доехала
      O "resetSW3";
      R "Robot12".TDI02    ;// палета не на месте

NETWORK
TITLE = DI3 Паллета на месте 2
      AN "SW2_1";//есть палета
      A "SW2_8";// есть захват
      S "Robot12".TDI03;//палета на месте


      A "SW2_1"            ;//нет палеты
      A "SW2_2"            ;// нет первого ряда
      AN "SW2_8"            ;// нет захвата
      AN "SW2_3"            ;//доехала
      O "resetSW2";
      R "Robot12".TDI03   ;// палета не на месте

NETWORK
TITLE = DI4 Захват открыт (продукт)
      A "SW12_4";
      = "Robot12".TDI04;

      A "SW12_5";
      = "Robot12".TDI05;

NETWORK
TITLE = DI6 Паллета отпущена захватом
      A "SW12_2";
      = "Robot12".TDI06;

      A "SW12_3";
      = "Robot12".TDI07;


NETWORK
TITLE = DI8 Наличие вакуума в присоске(объект захвачен)
      A "SW12_1";
      = "Robot12".TDI08;

NETWORK
TITLE = DI12 Наличие стопки паллет
      AN "SW1_3";
      = "Robot12".TDI12;



NETWORK
TITLE = DI13 Заблокировал смену стопки паллет
      A "Robot12".FDO12;
      S "Robot12".TDI13;

      A "Robot12".FDO13;
      R "Robot12".TDI13;


NETWORK
TITLE = DI15 Датчик 1го ряда линия 1
      AN "SW3_2";
      = "Robot12".TDI15;

NETWORK
TITLE = DI16 Датчик 1го ряда линия 2
      AN "SW2_2";
      = "Robot12".TDI16;

NETWORK
TITLE = DI19 Присоски опущены (датчик поиска)
      A "SW12_6";
      = "Robot12".TDI19;

      A "SW12_7";
      = "Robot12".TDI20;

NETWORK
TITLE = DI21 Картон в наличии
      A "SW19_1";
      = "Robot12".TDI21;

NETWORK
TITLE = EnableMove Разрешено двигаться
      O "Auto1";
      O "Manual1";
      AN "Error_Global";
      = "Robot12".UI3;
      = "Robot12".UI5;

NETWORK
TITLE = IMSTP Экстренный останов
      AN "Emergency_Security";
      = "Robot12".UI1;

NETWORK
TITLE = Hold временный останов
      O "Manual1";
      O(;
      A "Auto1";
 //   A     "System_started"
      );
      = "Robot12".UI2;
END_FUNCTION

