FUNCTION "ValveControl" : VOID
TITLE =ValveControl
//COMMAND'S
//
//1 = АВТОМАТ
//2 = РУЧНОЕ
//3 = ОТКРЫТЬ
//4 = ЗАКРЫТЬ
//5 = АСК
AUTHOR : knitall
FAMILY : Control
NAME : Valve
VERSION : 2.0


VAR_INPUT
  Valve : "VState";	
  NC : BOOL ;	
  Horn : BOOL ;	
END_VAR
VAR_IN_OUT
  Open : BOOL ;	
  Close : BOOL ;	
  Output : BOOL ;	
END_VAR
VAR_TEMP
  pAny : ANY ;	
  retval : INT ;	
  thises : "VState";	
  ARStore : DWORD ;	
  CommandOSS : BOOL ;	
END_VAR
BEGIN
NETWORK
TITLE =Load


//      TAR1  #ARStore
//      L     0
      L     P##Valve; 
      LAR1  ; 

      L     W [AR1,P#0.0]; //db num
      T     LW     4; 

      OPN   DB [LW 4]; 

      L     P#DBX 0.0; 
      L     W [AR1,P#4.0]; 
      +D    ; 
      LAR1  ; 

      L     DBD [AR1,P#0.0]; 
      T     LD    12; 

      L     DBD [AR1,P#4.0]; 
      T     LD    16; 

NETWORK
TITLE =Command


      A     #thises.IsLock; 
      JC    RCMD; 

      L     #thises.command; 
      L     0; // нет команды
      ==I   ; 
      JC    ECMD; 

      INC   1; // АВТОМАТ
      ==I   ; 
      S     #thises.IsAuto; 
      JC    RCMD; 

      INC   1; // РУЧНОЕ
      ==I   ; 
      R     #thises.IsAuto; 
      JC    RCMD; 

      INC   1; // ОТКРЫТЬ
      ==I   ; 
      S     #thises.Open; 
      R     #thises.Close; 
      JC    RCMD; 

      INC   1; // ЗАКРЫТЬ
      ==I   ; 
      S     #thises.Close; 
      R     #thises.Open; 
      JC    RCMD; 

      INC   1; // СБРОСИТЬ АВАРИЮ (АСК)
      ==I   ; 
      S     #thises.AckAlarm; 
      JC    RCMD; 

      INC   1; // Вывести из обслуживания
      ==I   ; 
      S     #CommandOSS; 

RCMD: L     0; 
      T     #thises.command; 

      AN    #thises.IsAuto; 
      =     #thises.IsManual; 

ECMD: CLR   ; 

NETWORK
TITLE =Обработка NC в начале


      A     #NC; 
      JC    NCDB; // NC detect at begin of block

      AN    #Output; 
      =     #Output; 

NCDB: CLR   ; 

NETWORK
TITLE =ЭТОТ НЕТВОРК НАДО УБИТЬ ПРИ ЗАГРУЗКЕ В КОНТРОЛЛЕР
//ИММИТАЦИЯ РАБОТЫ ВЕНТИЛЯ...
//////////////////////////////////////////////////
      A     #Output; //;
      =     #Open; //;
      AN    #Output; //;
      =     #Close; //;

//      SET   
//      R     #thises.output                // для данного проекта нет автоматов для клапанов

//////////////////////////////////////////////////
NETWORK
TITLE =Check state


//Is opened
      A     #Open; 
      AN    #Close; 
      A     #Output; 
      =     #thises.IsOpened; 

//Is closed 
      AN    #Open; 
      A     #Close; 
      AN    #Output; 
      =     #thises.IsClosed; 

NETWORK
TITLE =ПРОВЕРКА НА АВАРИЮ ОТКРЫТИЯ / ЗАКРЫТИЯ


      X     #thises.IsOpened; 
      X     #thises.IsClosed; 
      R     #thises.TimerOverflow; 
      R     #thises.TimerRunning; 
      JC    EXT; 
      A     #thises.TimerOverflow; 
      JC    EXT; 
      A     #thises.TimerRunning; 
      JC    TMR; 
      L     0; 
      T     #thises.count; 
      S     #thises.TimerRunning; 
TMR:  A     "Pulse_1_sec"; 
      JCN   EXT; 
      L     #thises.count; 
      L     B#16#1; 
      +I    ; 
      T     #thises.count; 
      CLR   ; 
      A     #Output; 
      JCN   CHKC; 
      L     #thises.countOpening; 
      JU    CMP; 
CHKC: L     #thises.countClosing; 
CMP:  >=I   ; 
      JCN   EXT; 
      R     #thises.TimerRunning; 
      S     #thises.TimerOverflow; 
EXT:  CLR   ; 

NETWORK
TITLE =Alarm


      A     #thises.AckAlarm; // если комманда сброса аварии
      R     #thises.IsAlarm; // сбрасываем аварийный бит

      A     #thises.TimerOverflow; 
      =     #thises.IsAlarm; 

      A     #thises.IsAlarm; 
      A     #Output; 
      AN    #thises.IsOpened; 
      =     #thises.AlarmOpening; 

      A     #thises.IsAlarm; 
      AN    #Output; 
      AN    #thises.IsClosed; 
      =     #thises.AlarmClosing; 

      A(    ; 
      A     #thises.IsAlarm; 
      A     #Horn; 
      AN    #thises.AckAlarm; 
      S     M     10.0; 
      )     ; 
      AN    #thises.existAlarm; 
 //     S     "ExistLevel"; 
      S     #thises.existAlarm; 

      AN    #thises.IsAlarm; 
      R     #thises.AckAlarm; 
      R     #thises.existAlarm; 

NETWORK
TITLE =Set valve position


//Open
      A     #Open; 
      =     #thises.IsOpened; 

//Close
      A     #Close; 
      =     #thises.IsClosed; 

NETWORK
TITLE =Color


// Reset color code
      L     0; 

// Alarm in process
      CLR   ; 
      A     #thises.TimerRunning; 
      JC    SALR; 

// Alarm opening
      AN    #thises.AlarmOpening; 
      JC    N1; 
      L     5; 
      JU    SALR; 

// Alarm closing
N1:   AN    #thises.AlarmClosing; 
      JC    N2; 
      L     6; 
      JU    SALR; 

// Valve is open
N2:   AN    #thises.IsOpened; 
      JC    N3; 
      L     1; 
      JU    CMAN; 

// Valve is close
N3:   AN    #thises.IsClosed; 
      JC    SALR; 
      L     3; 
      JU    CMAN; 

// Manual mode
CMAN: A     #thises.IsAuto; //Manual mode
      JC    SALR; 
      L     1; 
      +I    ; 

SALR: T     #thises.color; 

NETWORK
TITLE =Control in auto


      A     #thises.IsAuto; 
      JCN   ECAM; 

      A     #thises.output; 
      =     #Output; 

ECAM: CLR   ; 

NETWORK
TITLE =Control in manual


      AN    #thises.IsAuto; 
      JCN   ECMM; 

      AN    #thises.forceClose; 
      A(    ; 
      O     #thises.Open; 
      O     #thises.forceOpen; 
      )     ; 
      S     #Output; 

      A(    ; 
      O     #thises.Close; 
      O     #thises.forceClose; 
      )     ; 
      R     #Output; 

ECMM: CLR   ; 

NETWORK
TITLE =Global manual 


//Если управление идет с тумблеров или вообще руками крутят
      A     M      2.0; 
      R     #Output; 

NETWORK
TITLE =Обработка NC в конце


      A     #NC; 
      JC    NCDE; // NC detect at end of block

      AN    #Output; 
      =     #Output; 

NCDE: CLR   ; 

NETWORK
TITLE =OOS


      A     #CommandOSS; 
      R     #CommandOSS; 
      JCN   NOOS; 

      AN    #thises.IsOOS; 
      =     #thises.IsOOS; 

NOOS: CLR   ; 

NETWORK
TITLE =Stop button pressed


 //     A     "Emergy Stop"; 
 //     R     #Output; 

NETWORK
TITLE =Save


      L     LD    12; 
      T     DBD [AR1,P#0.0]; 

      L     LD    16; 
      T     DBD [AR1,P#4.0]; 

      LAR1  #ARStore; 

END_FUNCTION

